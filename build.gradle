plugins {
    id "java"
    id "java-library"
    id "com.jfrog.artifactory" version "4.5.4"
    id "com.jfrog.bintray" version "1.7.3"
    id "maven-publish"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

// run gradle with "-Dsnapshot=true" to automatically append "-SNAPSHOT" to the version
version = '1.3.0' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
sourceCompatibility = 1.8

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
    docx4JVersion = System.getProperty("docx4j.version") ? System.getProperty("docx4j.version") : '6.1.2'
}

dependencies {
    implementation("commons-io:commons-io:2.5")
    implementation("org.javassist:javassist:3.21.0-GA")
    implementation("org.springframework:spring-expression:4.3.6.RELEASE")

    // We want docx-stamper to be usable with any docx4j version, so the dependency to docx4J
    // has to be provided by the client.
    compileOnly("org.docx4j:docx4j:${docx4JVersion}")
    
    testImplementation("org.docx4j:docx4j:${docx4JVersion}")    
    testImplementation("junit:junit:4.4")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "thombergs"
            name "Tom Hombergs"
            email "tom.hombergs@gmail.com"
        }
    }

    scm {
        url "https://github.com/thombergs/docx-stamper"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'org.wickedsource.docx-stamper'
            artifactId 'docx-stamper'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Template engine for .docx documents.')
                root.appendNode('name', 'docx-stamper')
                root.appendNode('url', 'https://github.com/thombergs/docx-stamper')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
            properties = [
                    'build.number': buildNumber,
                    'build.name': 'docx-stamper'
            ]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(buildNumber)
    clientConfig.info.setBuildName('docx-stamper')
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenPublication']

    pkg {
        repo = 'maven-releases'
        name = 'docx-stamper'
        userOrg = 'reflectoring'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/thombergs/docx-stamper'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released  = new Date()
        }
    }

    publish = true
}


